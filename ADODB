Option Explicit
Public objConnnection As Object
Public objRecordset As Object
Public objCommand As Object
Const adLockOptimistic = 3
Const adOpenStatic = 3
Const adCmdText = 1
Const adStateOpen = 1
Private Sub Class_Initialize()
    Dim strConnectionString As String
    Dim strFullName As String
    Dim strPfad As String
    Dim strAccessDB As String

    Set objConnnection = CreateObject("ADODB.Connection")
    Set objRecordset = CreateObject("ADODB.RecordSet")
    Set objCommand = CreateObject("ADODB.Command")

    strPfad = ThisWorkbook.Path
    strAccessDB = ThisWorkbook.Names("AccessDB").RefersToRange.Value
    strFullName = strPfad & "\" & strAccessDB

    strConnectionString = "PROVIDER=Microsoft.ACE.OLEDB.12.0;Data Source=" & strFullName & ";"
    objConnnection.ConnectionString = strConnectionString
    objConnnection.Open

    objRecordset.ActiveConnection = objConnnection
    objRecordset.LockType = adLockOptimistic
    objRecordset.CursorType = adOpenStatic

    objCommand.ActiveConnection = objConnnection
    objCommand.CommandType = adCmdText
End Sub
Private Sub Class_Terminate()
    If objRecordset.State = adStateOpen Then
        objRecordset.Close
    End If

    objConnnection.Close

    Set objRecordset = Nothing
    Set objCommand = Nothing
    Set objConnnection = Nothing
End Sub

Private Sub cmdBtnLos_Click()
    Dim objDatenbank As clsAccessDB
    Dim strSQL As String
    Dim objWorksheet As Excel.Worksheet

    Set objDatenbank = New clsAccessDB
    strSQL = "SELECT * from qryEinahmeMitKW"
    objDatenbank.objRecordset.Source = strSQL
    objDatenbank.objRecordset.Open
    Set objWorksheet = ThisWorkbook.Worksheets(strBlattName)
    objWorksheet.Range("A2").CopyFromRecordset objDatenbank.objRecordset
End Sub
